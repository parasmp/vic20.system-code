* Comments by Willi Kusche
;.PAGE 'VIC OPERATING SYSTEM - PART 1  "SVO1"  8/21/82'
	INCLUDE	VMMAP.h
CSCBGN	EQU	$9400
SCRWID	EQU	22
LINWID	EQU	88
SCDPTH	EQU	23
;
;	ENTRY POINTS
;
*	.ENT	AE1E1
*	.ENT	INTSVC
*	.ENT	KEYBIP
*	.ENT	PLOT
*	.ENT	RDIOAD
*	.ENT	RDSPRM
*	.ENT	SCANKB
*	.ENT	SCKBIP
*	.ENT	SCROP
*	.ENT	VAE4A0
*	.ENT	VAE4A9
*	.ENT	VAE4B2
*	.ENT	VAE4BC
*	.ENT	VAE4C1
*	.ENT	VAE4CF
*	.ENT	VAE5C3
;
;	OP SYS PART 2 REFERENCES
;
AF3FF	EQU	$F647
AF422	EQU	$F66A
AF8B9	EQU	$F8E3
UPDCLK	EQU	$FFEA
VAF39E	EQU	$F39E
;.PAGE 'PART 2 OVERFLOW'
	ORG	$E480
;
;
;
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
;
;
;
VAE4A0	LDA	A912C
	AND	#$DF
	STA	A912C
	RTS
;
;
;
VAE4A9	LDA	A912C
	ORA	#$20
	STA	A912C
	RTS
;
;
;
VAE4B2	LDA	A911F
	CMP	A911F
	BNE	VAE4B2
	LSR	A
	RTS
;
;
;
VAE4BC	LDX	IOOPT
	JMP	AF3FF
;
;
;
VAE4C1	TXA
	BNE	VOSAB
	LDA	VZC3
	STA	SAVEND
	LDA	VZC3+1
	STA	SAVEND+1
VOSAB	JMP	AF422
;
;
;
VAE4CF	JSR	AF8B9
	BCC	VOSAC
	PLA
	LDA	#$00
VOSAC	JMP	VAF39E
;
;
;
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF
;.PAGE 'VECTORED ROUTINES'
;
;
;
RDIOAD	LDX	#>A9110
	LDY	#<A9110
	RTS
;
;
;
RDSPRM	LDX	#SCRWID
	LDY	#SCDPTH
	RTS
;
;
;
PLOT	BCS	VOSAD
	STX	SLINNO
	STY	SCRNDX
	JSR	SSCLNP
VOSAD	LDX	SLINNO
	LDY	SCRNDX
	RTS
;.PAGE 'SCREEN EDITOR COLD START'
;
;
;
AE1E1	JSR	VAE5BB
	LDA	SCRMPG
	AND	#$FD
	ASL	A
	ASL	A
	ORA	#$80
	STA	A9005
	LDA	SCRMPG
	AND	#$02
	BEQ	VOSAE
	LDA	#$80
	ORA	A9002
	STA	A9002
VOSAE	LDA	#$00
	STA	V0291
	STA	CUFLFL
	LDA	#>XLATE1
	STA	V028F
	LDA	#<XLATE1
	STA	V028F+1
	LDA	#10
	STA	V0289
	STA	V028C
	LDA	#6
	STA	CURHUE
	LDA	#4
	STA	V028B
	LDA	#$0C
	STA	BLKCLK
	STA	CBLINK
;
;	SET ADDRESS/WRAP ARRAY
;
CLRSCR	LDA	SCRMPG
	ORA	#$80
	TAY
	LDA	#$00
	TAX
VOSAG	STY	A0229,X
	CLC
	ADC	#SCRWID
	BCC	VOSAF
	INY
VOSAF	INX
	CPX	#24	; #SCDPTH+1
	BNE	VOSAG
	LDA	#$FF
	STA	A0229,X
;
;	CLEAR SCREEN
;
	LDX	#SCDPTH-1
QCK	JSR	VAEA8D
	DEX
	BPL	QCK
;
;	POINT TO FIRST SCREEN LINE
;
SSC1ST	LDY	#0
	STY	SCRNDX
	STY	SLINNO
;
;	SET UP SCREEN LINE PARAMETERS
;
SSCLNP	LDX	SLINNO
	LDA	SCRNDX
VOSAJ	LDY	A0229,X
	BMI	VOSAI
	CLC
	ADC	#SCRWID
	STA	SCRNDX
	DEX
	BPL	VOSAJ
VOSAI	LDA	A0229,X
	AND	#$03
	ORA	SCRMPG
	STA	SCRPTR+1
	LDA	SLNADR,X
	STA	SCRPTR
	LDA	#SCRWID-1
	INX
VOSAL	LDY	A0229,X
	BMI	VOSAK
	CLC
	ADC	#SCRWID
	INX
	BPL	VOSAL
VOSAK	STA	SWRPFL
	RTS
;
;
;
VAE5B5	JSR	VAE5BB
	JMP	SSC1ST
;
;
;
VAE5BB	LDA	#SCREEN
	STA	OUTDVC
	LDA	#KEYBD
	STA	INDVC
VAE5C3	LDX	#16
VOSAM	LDA	VAEDE4-1,X
	STA	A9000-1,X
	DEX
	BNE	VOSAM
	RTS
;.PAGE 'GET CHARACTER FROM KEYBOARD BUFFER'
;
;	SAVE FIRST BUFFER CHARACTER IN 'Y'
;
KEYBIP	LDY	KEYBUF
;
;	SHIFT BUFFER CHARACTERS LEFT
;
	LDX	#0
PCN	LDA	KEYBUF+1,X
	STA	KEYBUF,X
	INX
	CPX	KEYCNT
	BNE	PCN
;
;	REDUCE BUFFER COUNT BY ONE
;
	DEC	KEYCNT
;
;	LEAVE CHARACTER IN 'A'
;
	TYA
	CLI
	CLC
	RTS
;.PAGE 'GET CHARACTER FROM KEYBOARD OR SCREEN'
;
;	DISPLAY CHARACTER ON SCREEN
;
AE294	JSR	SCROP
;
;	WAIT FOR KEYSTROKE
;
PEZ	LDA	KEYCNT
	STA	CBLINK
	STA	V0292
	BEQ	PEZ
;
;
;
	SEI
	LDA	CUFLFL
	BEQ	PFA
	LDA	CHUCUR
	LDX	COUCUR
	LDY	#0
	STY	CUFLFL
;
;	PUT CHARACTER ON SCREEN
;
	JSR	AE7AC
;
;	GET CHARACTER FROM KEYBOARD BUFFER
;
PFA	JSR	KEYBIP
;
;	TEST FOR 'RUN' CHARACTER
;
	CMP	#$83
	BNE	PFC
;
;	PUT 'RUN' COMMAND SEQUENCE INTO BUFFER
;
	LDX	#9
	SEI
	STX	KEYCNT
PFB	LDA	RUNLDK-1,X
	STA	KEYBUF-1,X
	DEX
	BNE	PFB
	BEQ	PEZ
;
;	TEST FOR RETURN CHARACTER
;
PFC	CMP	#$0D
	BNE	AE294
	LDY	SWRPFL
	STY	SCKBFL
;
;	STRIP TRAILING SPACES
;
PFD	LDA	(SCRPTR),Y
	CMP	#SPACE
	BNE	PFE
	DEY
	BNE	PFD
PFE	INY
	STY	SIPLIM
	LDY	#0
	STY	V0292
	STY	SCRNDX
	STY	SQUOFL
	LDA	A0220
	BMI	PFF
	LDX	SLINNO
	JSR	VAE719
	CPX	A0220
	BNE	PFF
	BNE	PFF
	LDA	A0221
	STA	SCRNDX
	CMP	SIPLIM
	BCC	PFF
	BCS	AE327
;
;	ENTRY POINT - SAVE REGISTERS
;
SCKBIP	TYA
	PHA
	TXA
	PHA
;
;	TEST SOURCE FLAG
;
	LDA	SCKBFL
	BEQ	PEZ
;
;	GET CHARACTER FROM SCREEN MEMORY
;
PFF	LDY	SCRNDX
	LDA	(SCRPTR),Y
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	STA	HOLDCH
;
;	CONVERT TO PET ASCII
;
	AND	#$3F
	ASL	HOLDCH
	BIT	HOLDCH
	BPL	PFH
	ORA	#$80
PFH	BCC	PFJ
	LDX	SQUOFL
	BNE	PFK
PFJ	BVS	PFK
	ORA	#$40
PFK	INC	SCRNDX
	JSR	XOQUFL
	CPY	SIPLIM
	BNE	PFM
AE327	LDA	#$00
	STA	SCKBFL
	LDA	#$0D
	LDX	INDVC
	CPX	#SCREEN
	BEQ	VOSBH
	LDX	OUTDVC
	CPX	#SCREEN
	BEQ	PFL
VOSBH	JSR	SCROP
PFL	LDA	#$0D
;
;	RESTORE REGISTERS
;
PFM	STA	HOLDCH
	PLA
	TAX
	PLA
	TAY
	LDA	HOLDCH
;
;	TEST FOR 'PI' CHARACTER
;
	CMP	#$DE
	BNE	PFN
	LDA	#$FF
PFN	CLC
	RTS
;.PAGE 'ASSORTED ROUTINES'
;
;	FLIP FLOP QUOTE FLAG IF QUOTE
;
XOQUFL	CMP	#'"
	BNE	PFP
	LDA	SQUOFL
	EOR	#$01
	STA	SQUOFL
	LDA	#'"
PFP	RTS
;
;	SCREEN OUTPUT FINAL PROCESSING
;
AE356	ORA	#$40
AE358	LDX	SREVFL
	BEQ	PGE
AE35D	ORA	#$80
PGE	LDX	CHICNT
	BEQ	PGF
	DEC	CHICNT
PGF	LDX	CURHUE
	JSR	AE7AC
	JSR	VAE6EA
;
;	REGISTER RESTORE FOR SCREEN OUTPUT
;
SCOPEX	PLA
	TAY
	LDA	CHICNT
	BEQ	PGD
	LSR	SQUOFL
PGD	PLA
	TAX
	PLA
	CLC
	CLI
	RTS
;
;
;
VAE6EA	JSR	VAE8FA
	INC	SCRNDX
	LDA	SWRPFL
	CMP	SCRNDX
	BCS	VOSBO
	CMP	#LINWID-1
	BEQ	VOSBP
	LDA	V0292
	BEQ	VOSBQ
	JMP	VAE9F0
VOSBQ	LDX	SLINNO
	CPX	#SCDPTH
	BCC	VAE70E
	JSR	SCROLU
	DEC	SLINNO
	LDX	SLINNO
VAE70E	ASL	A0229,X
	LSR	A0229,X
	JMP	PATCH
RETURN	ADC	#SCRWID
	STA	SWRPFL
VAE719	LDA	A0229,X
	BMI	VOSBS
	DEX
	BNE	VAE719
VOSBS	JMP	VAEA7E
;
;
;
VOSBP	DEC	SLINNO
	JSR	AE530
	LDA	#0
	STA	SCRNDX
VOSBO	RTS
;
;
;
AE3C4	LDX	SLINNO
	BNE	PGB
	STX	SCRNDX
	PLA
	PLA
	BNE	SCOPEX
PGB	DEX
	STX	SLINNO
	JSR	SSCLNP
	LDY	SWRPFL
	STY	SCRNDX
	RTS
;.PAGE 'SCREEN CHARACTER OUTPUT'
;
;	SAVE REGISTERS
;
SCROP	PHA
	STA	HOLDCH
	TXA
	PHA
	TYA
	PHA
;
;	CLEAR FLAG
;
	LDA	#0
	STA	SCKBFL
;
;
;
	LDY	SCRNDX
;
;	TEST IF HIGH ORDER BIT SET
;
	LDA	HOLDCH
	BPL	PCT
	JMP	AE48F
;
;	TEST FOR 'RETURN' CHARACTER
;
PCT	CMP	#$0D
	BNE	PCV
	JMP	SSCRNL
;
;	TEST FOR CONTROL CHARACTER
;
PCV	CMP	#SPACE
	BCC	PGH
	CMP	#$60
	BCC	VOSBZ
	AND	#$DF
	BNE	VOSCA
VOSBZ	AND	#$3F
VOSCA	JSR	XOQUFL
	JMP	AE358
;
;	TEST COUNT OF CHARACTERS INSERTED
;
PGH	LDX	CHICNT
	BEQ	PGJ
	JMP	AE35D
;
;	TEST FOR 'DELETE' CHARACTER
;
PGJ	CMP	#$14
	BNE	PGM
	TYA
	BNE	VOSCD
	JSR	AE3C4
	JMP	PGL
VOSCD	JSR	VAE8E8
	DEY
	STY	SCRNDX
	JSR	VAEAB2
PGK	INY
	LDA	(SCRPTR),Y
	DEY
	STA	(SCRPTR),Y
	INY
	LDA	(COSPTR),Y
	DEY
	STA	(COSPTR),Y
	INY
	CPY	SWRPFL
	BNE	PGK
PGL	LDA	#SPACE
	STA	(SCRPTR),Y
	LDA	CURHUE
	STA	(COSPTR),Y
	BPL	J1EX
;
;
;
PGM	LDX	SQUOFL
	BEQ	PGN
	JMP	AE35D
;
;	TEST FOR 'REVERSE ON' CHARACTER
;
PGN	CMP	#$12
	BNE	PGP
	STA	SREVFL
;
;	TEST FOR 'HOME' CHARACTER
;
PGP	CMP	#$13
	BNE	PGQ
	JSR	SSC1ST
;
;	TEST FOR 'CURSOR RIGHT' CHARACTER
;
PGQ	CMP	#$1D
	BNE	PGS
	INY
	JSR	VAE8FA
	STY	SCRNDX
	DEY
	CPY	SWRPFL
	BCC	PGR
	DEC	SLINNO
	JSR	AE530
	LDY	#0
PGT	STY	SCRNDX
PGR	JMP	SCOPEX
;
;	TEST FOR 'CURSOR DOWN' CHARACTER
;
PGS	CMP	#$11
	BNE	VOSCL
	CLC
	TYA
	ADC	#SCRWID
	TAY
	INC	SLINNO
	CMP	SWRPFL
	BCC	PGT
	BEQ	PGT
	DEC	SLINNO
VOSCP	SBC	#SCRWID
	BCC	VOSCO
	STA	SCRNDX
	BNE	VOSCP
VOSCO	JSR	AE530
J1EX	JMP	SCOPEX
;
;	SET COLOR IF COLOR KEY
;
VOSCL	JSR	VAE912
;
;	CONTINUE
;
	JMP	VAED21
;
;	PROCESS X'80' TO X'FF' CHARACTERS
;
AE48F	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	AND	#$7F
;
;	TEST FOR 'PI' CHARACTER
;
	CMP	#$7F
	BNE	PGV
	LDA	#$5E
;
;	TEST FOR CONTROL CHARACTERS
;
PGV	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	CMP	#SPACE
	BCC	PGW
	JMP	AE356
;
;	TEST FOR 'SHIFTED RETURN' CHARACTER
;
PGW	CMP	#CR
	BNE	PGX
	JMP	SSCRNL
;
;
;
PGX	LDX	SQUOFL
	BNE	PGZ
;
;	TEST FOR 'INSERT' CHARACTER
;
	CMP	#$14
	BNE	PHA
	LDY	SWRPFL
	LDA	(SCRPTR),Y
	CMP	#SPACE
	BNE	PHB
	CPY	SCRNDX
	BNE	PHC
PHB	CPY	#LINWID-1
	BEQ	VOSCX
	JSR	SCROLD
PHC	LDY	SWRPFL
	JSR	VAEAB2
PHE	DEY
	LDA	(SCRPTR),Y
	INY
	STA	(SCRPTR),Y
	DEY
	LDA	(COSPTR),Y
	INY
	STA	(COSPTR),Y
	DEY
	CPY	SCRNDX
	BNE	PHE
	LDA	#SPACE
	STA	(SCRPTR),Y
	LDA	CURHUE
	STA	(COSPTR),Y
	INC	CHICNT
VOSCX	JMP	SCOPEX
;
;
;
PHA	LDX	CHICNT
	BEQ	PHG
PGZ	ORA	#$40
	JMP	AE35D
;
;	TEST FOR 'CURSOR UP' CHARACTER
;
PHG	CMP	#$11
	BNE	PHH
	LDX	SLINNO
	BEQ	VOSDB
	DEC	SLINNO
	LDA	SCRNDX
	SEC
	SBC	#SCRWID
	BCC	VOSDC
	STA	SCRNDX
	BPL	VOSDB
VOSDC	JSR	SSCLNP
	BNE	VOSDB
;
;	TEST FOR 'REVERSE OFF' CHARACTER
;
PHH	CMP	#$12
	BNE	PHQ
	LDA	#0
	STA	SREVFL
;
;	TEST FOR 'CURSOR LEFT' CHARACTER
;
PHQ	CMP	#$1D
	BNE	PHR
	TYA
	BEQ	VOSDH
	JSR	VAE8E8
	DEY
	STY	SCRNDX
	JMP	SCOPEX
VOSDH	JSR	AE3C4
	JMP	SCOPEX
;
;	TEST FOR 'CLEAR SCREEN' CHARACTER
;
PHR	CMP	#$13
	BNE	VOSDI
	JSR	CLRSCR
VOSDB	JMP	SCOPEX
;
;	SET COLOR IF COLOR KEY
;
VOSDI	ORA	#$80
	JSR	VAE912
;
;	CONTINUE
;
	JMP	VAED30
;.PAGE 'SUBROUTINES'
;
;
;
AE530	LSR	A0220
	LDX	SLINNO
VOSDK	INX
	CPX	#SCDPTH
	BNE	PHU
	JSR	SCROLU
PHU	LDA	A0229,X
	BPL	VOSDK
	STX	SLINNO
	JMP	SSCLNP
;
;	SET UP FOR NEW SCREEN LINE
;
SSCRNL	LDX	#0
	STX	CHICNT
	STX	SREVFL
	STX	SQUOFL
	STX	SCRNDX
	JSR	AE530
	JMP	SCOPEX
;
;
;
VAE8E8	LDX	#$04
	LDA	#$00
VOSDM	CMP	SCRNDX
	BEQ	VOSDL
	CLC
	ADC	#SCRWID
	DEX
	BNE	VOSDM
	RTS
VOSDL	DEC	SLINNO
	RTS
;
;
;
VAE8FA	LDX	#$04
	LDA	#SCRWID-1
VOSDO	CMP	SCRNDX
	BEQ	VOSDN
	CLC
	ADC	#SCRWID
	DEX
	BNE	VOSDO
	RTS
VOSDN	LDX	SLINNO
	CPX	#SCDPTH
	BEQ	VOSDP
	INC	SLINNO
VOSDP	RTS
;
;	SET COLOR (0 TO 7)
;
VAE912	LDX	#7
VOSDR	CMP	VAE921,X
	BEQ	VOSDQ
	DEX
	BPL	VOSDR
	RTS
VOSDQ	STX	CURHUE
	RTS
;
;	COLOR KEY CODES
;
VAE921	DB	$90,$05,$1C,$9F,$9C,$1E,$1F,$9E
;.PAGE 'MYSTERY DATA'
	DB	$EF,$A1,$DF,$A6,$E1,$B1,$E2,$B2
	DB	$E3,$B3,$E4,$B4,$E5,$B5,$E6,$B6
	DB	$E7,$B7,$E8,$B8,$E9,$B9,$FA,$BA
	DB	$FB,$BB,$FC,$BC,$EC,$BD,$FE,$BE
	DB	$84,$BF,$F7,$C0,$F8,$DB,$F9,$DD
	DB	$EA,$DE,$5E,$E0,$5B,$E1,$5D,$E2
	DB	$40,$B0,$61,$B1,$78,$DB,$79,$DD
	DB	$66,$B6,$77,$C0,$70,$F0,$71,$F1
	DB	$72,$F2,$73,$F3,$74,$F4,$75,$F5
	DB	$76,$F6,$7D,$FD
;.PAGE 'SCROLL UP'
;
;
;
SCROLU	LDA	SCMPTR
	PHA
	LDA	SCMPTR+1
	PHA
	LDA	SAVEND
	PHA
	LDA	SAVEND+1
VAE980	PHA
VOSDZ	LDX	#$FF
	DEC	SLINNO
	DEC	A0220
	DEC	VZF2
VOSDW	INX
	JSR	VAEA7E
	CPX	#SCDPTH-1
	BCS	VOSDV
	LDA	SLNADR+1,X
	STA	SCMPTR
	LDA	A0229+1,X
	JSR	VAEA56
	BMI	VOSDW
VOSDV	JSR	VAEA8D
	LDX	#$00
VOSDY	LDA	A0229,X
	AND	#$7F
	LDY	A0229+1,X
	BPL	VOSDX
	ORA	#$80
VOSDX	STA	A0229,X
	INX
	CPX	#SCDPTH-1
	BNE	VOSDY
	LDA	A0229+22
	ORA	#$80
	STA	A0229+22
	LDA	A0229
	BPL	VOSDZ
VOSDT	INC	SLINNO
	INC	VZF2
	LDA	#$FB
	STA	A9120
	LDA	A9121
	CMP	#$FE
	PHP
	LDA	#$F7
	STA	A9120
	PLP
	BNE	PIG
	LDY	#$00
VOSEB	NOP
	DEX
	BNE	VOSEB
	DEY
	BNE	VOSEB
	STY	KEYCNT
PIG	LDX	SLINNO
	PLA
	STA	SAVEND+1
	PLA
	STA	SAVEND
	PLA
	STA	SCMPTR+1
	PLA
	STA	SCMPTR
	RTS
;.PAGE 'SCROLL DOWN'
;
;
;
SCROLD	LDX	SLINNO
VAE9F0	INX
	LDA	A0229,X
	BPL	VAE9F0
	STX	VZF2
	CPX	#SCDPTH-1
	BEQ	VOSEE
	BCC	VOSEE
	JSR	SCROLU
	LDX	VZF2
	DEX
	DEC	SLINNO
	JMP	VAE70E
VOSEE	LDA	SCMPTR
	PHA
	LDA	SCMPTR+1
	PHA
	LDA	SAVEND
	PHA
	LDA	SAVEND+1
	PHA
	LDX	#SCDPTH
VOSEI	DEX
	JSR	VAEA7E
	CPX	VZF2
	BCC	VOSEG
	BEQ	VOSEG
	LDA	SLNADR-1,X
	STA	SCMPTR
	LDA	A0229-1,X
	JSR	VAEA56
	BMI	VOSEI
VOSEG	JSR	VAEA8D
	LDX	#SCDPTH-2
VOSEL	CPX	VZF2
	BCC	VOSEJ
	LDA	A0229+1,X
	AND	#$7F
	LDY	A0229,X
	BPL	VOSEK
	ORA	#$80
VOSEK	STA	A0229+1,X
	DEX
	BNE	VOSEL
VOSEJ	LDX	VZF2
	JSR	VAE70E
	PLA
	STA	SAVEND+1
	PLA
	STA	SAVEND
	PLA
	STA	SCMPTR+1
	PLA
	STA	SCMPTR
	RTS
;.PAGE 'SUBROUTINES'
;
;
;
VAEA56	AND	#$03
	ORA	SCRMPG
	STA	SCMPTR+1
	JSR	VAEA6E
	LDY	#SCRWID-1
VOSEM	LDA	(SCMPTR),Y
	STA	(SCRPTR),Y
	LDA	(SAVEND),Y
	STA	(COSPTR),Y
	DEY
	BPL	VOSEM
	RTS
;
;
;
VAEA6E	JSR	VAEAB2
	LDA	SCMPTR
	STA	SAVEND
	LDA	SCMPTR+1
	AND	#$03
	ORA	#<CSCBGN
	STA	SAVEND+1
	RTS
;
;	CONVERT LINE NUMBER TO ADDRESS
;
VAEA7E	LDA	SLNADR,X
	STA	SCRPTR
	LDA	A0229,X
	AND	#$03
	ORA	SCRMPG
	STA	SCRPTR+1
	RTS
;
;	CLEAR SCREEN LINE
;
VAEA8D	LDY	#SCRWID-1
	JSR	VAEA7E
	JSR	VAEAB2
VOSEN	LDA	#SPACE
	STA	(SCRPTR),Y
	LDA	#$01
	STA	(COSPTR),Y
	DEY
	BPL	VOSEN
	RTS
;
;	STORE CHARACTER AND COLOR
;
AE7AC	TAY
	LDA	#$02
	STA	BLKCLK
	JSR	VAEAB2
	TYA
VAEAAA	LDY	SCRNDX
	STA	(SCRPTR),Y
	TXA
	STA	(COSPTR),Y
	RTS
;
;	BUILD COLOR SCREEN ADDRESS
;
VAEAB2	LDA	SCRPTR
	STA	COSPTR
	LDA	SCRPTR+1
	AND	#$03
	ORA	#<CSCBGN
	STA	COSPTR+1
	RTS
;.PAGE 'INTERRUPT SERVICE ROUTINE'
;
;	UPDATE SYSTEM CLOCK
;
INTSVC	JSR	UPDCLK
;
;	TEST IF CURSOR BLINK ACTIVE
;
	LDA	CBLINK
	BNE	VOSEO
;
;	COUNT DOWN BLINK CLOCK
;
	DEC	BLKCLK
	BNE	VOSEO
;
;	SET BLINK CLOCK
;
	LDA	#$14
	STA	BLKCLK
;
;	BLINK CURSOR
;
	LDY	SCRNDX
	LSR	CUFLFL
	LDX	COUCUR
	LDA	(SCRPTR),Y
	BCS	PCW
	INC	CUFLFL
	STA	CHUCUR
	JSR	VAEAB2
	LDA	(COSPTR),Y
	STA	COUCUR
	LDX	CURHUE
	LDA	CHUCUR
PCW	EOR	#$80
	JSR	VAEAAA
;
;
;
VOSEO	LDA	A911F
	AND	#$40
	BEQ	VOSER
	LDY	#$00
	STY	A0207
	LDA	A911C
	ORA	#$02
	BNE	VOSES
VOSER	LDA	A0207
	BNE	VOSET
	LDA	A911C
	AND	#$FD
VOSES	BIT	A911E
	BVS	VOSET
	STA	A911C
VOSET	JSR	SCANKB
	BIT	A9124
	PLA
	TAY
	PLA
	TAX
	PLA
	RTI
;
;
;
SCANKB	LDA	#$00
	STA	V028D
	LDY	#$40
	STY	KBCODE
	STA	A9120
	LDX	A9121
	CPX	#$FF
	BEQ	VOSEV
	LDA	#$FE
	STA	A9120
	LDY	#$00
	LDA	#>KBXLAT
	STA	VZF5
	LDA	#<KBXLAT
	STA	VZF5+1
VOSEW	LDX	#$08
	LDA	A9121
	CMP	A9121
	BNE	VOSEW
VOSFC	LSR	A
	BCS	VOSEX
	PHA
	LDA	(VZF5),Y
	CMP	#$05
	BCS	VOSEY
	CMP	#$03
	BEQ	VOSEY
	ORA	V028D
	STA	V028D
	BPL	VOSFA
VOSEY	STY	KBCODE
VOSFA	PLA
VOSEX	INY
	CPY	#65
	BCS	VOSFB
	DEX
	BNE	VOSFC
	SEC
	ROL	A9120
	BNE	VOSEW
VOSFB	JMP	(V028F)
;
;
;
XLATE2	LDY	KBCODE
	LDA	(VZF5),Y
	TAX
	CPY	VZC5
	BEQ	VOSFE
	LDY	#$10
	STY	V028C
	BNE	VOSFF
VOSFE	AND	#$7F
	BIT	V028A
	BMI	VOSFG
	BVS	VOSFH
	CMP	#$7F
VOSEV	BEQ	VOSFF
	CMP	#$14
	BEQ	VOSFG
	CMP	#$20
	BEQ	VOSFG
	CMP	#$1D
	BEQ	VOSFG
	CMP	#$11
	BNE	VOSFH
VOSFG	LDY	V028C
	BEQ	VOSFN
	DEC	V028C
	BNE	VOSFH
VOSFN	DEC	V028B
	BNE	VOSFH
	LDY	#$04
	STY	V028B
	LDY	KEYCNT
	DEY
	BPL	VOSFH
VOSFF	LDY	KBCODE
	STY	VZC5
	LDY	V028D
	STY	V028E
	CPX	#$FF
	BEQ	VOSFH
	TXA
	LDX	KEYCNT
	CPX	V0289
	BCS	VOSFH
	STA	KEYBUF,X
	INX
	STX	KEYCNT
VOSFH	LDA	#$F7
	STA	A9120
	RTS
;
;
;
XLATE1	LDA	V028D
	CMP	#$03
	BNE	VOSFT
	CMP	V028E
	BEQ	VOSFH
	LDA	V0291
	BMI	VOSFV
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	LDA	A9005
	EOR	#$02
	STA	A9005
	NOP
	NOP
	NOP
	NOP
	JMP	VOSFV
VOSFT	ASL	A
	CMP	#$08
	BCC	VOSFW
	LDA	#$06
	NOP
	NOP
VOSFW	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	TAX
	LDA	VAEC46,X
	STA	VZF5
	LDA	VAEC46+1,X
	STA	VZF5+1
VOSFV	JMP	XLATE2
;.PAGE 'CONSTANTS'
;
;
;
VAEC46	DW	KBXLAT
	DW	KBXLT2
	DW	KBXLT3
	DW	KBXLT4
	DW	KBXLAT
	DW	KBXLT2
	DW	VAED69
	DW	KBXLT4
	DW	VAED21
	DW	VAED69
	DW	VAED69
	DW	KBXLT4
;
;
;
KBXLAT	ASC	'13579+\'
	DB	$14
	DB	$5F
	ASC	'WRYIP*'
	DB	$0D
	DB	$04
	ASC	'ADGJL;'
	DB	$1D
	DB	$03,$01
	ASC	'XVN,/'
	DB	$11
	ASC	' ZCBM.'
	DB	$01,$85
	DB	$02
	ASC	'SFHK:='
	DB	$86
	ASC	'QETUO@^'
	DB	$87
	ASC	'24680-'
	DB	$13,$88
	DB	$FF
;
;	SHIFT KEY DEPRESSED
;
KBXLT2	ASC	'!#%'')'
	DB	$DB,$A9,$94
	DB	$5F,$D7,$D2,$D9,$C9,$D0,$C0,$8D
	DB	$04,$C1,$C4,$C7,$CA,$CC
	ASC	']'
	DB	$9D
	DB	$83,$01,$D8,$D6,$CE
	ASC	'<?'
	DB	$91
	DB	$A0,$DA,$C3,$C2,$CD
	ASC	'>'
	DB	$01,$89
	DB	$02,$D3,$C6,$C8,$CB
	ASC	'[='
	DB	$8A
	DB	$D1,$C5,$D4,$D5,$CF,$BA,$DE,$8B
	ASC	'"$&(0'
	DB	$DD,$93,$8C
	DB	$FF
;
;	CBM KEY PRESSED
;
KBXLT3	ASC	'!#%'')'
	DB	$A6,$A8,$94
	DB	$5F,$B3,$B2,$B7,$A2,$AF,$DF,$8D
	DB	$04,$B0,$AC,$A5,$B5,$B6
	ASC	']'
	DB	$9D
	DB	$83,$01,$BD,$BE,$AA
	ASC	'<?'
	DB	$91
	DB	$A0,$AD,$BC,$BF,$A7
	ASC	'>'
	DB	$01,$89
	DB	$02,$AE,$BB,$B4,$A1
	ASC	'[='
	DB	$8A
	DB	$AB,$B1,$A3,$B8,$B9,$A4,$DE,$8B
	ASC	'"$&(0'
	DB	$DC,$93,$8C
	DB	$FF
;.PAGE 'SCREEN CHARACTER OUTPUT CONTINUED'
;
;	TEST FOR 'SET TEXT MODE' CHARACTER
;
VAED21	CMP	#$0E
	BNE	VAED30
	LDA	#$02
	ORA	A9005
	STA	A9005
	JMP	SCOPEX
;
;	TEST FOR 'SET GRAPHICS MODE' CHARACTER
;
VAED30	CMP	#$8E
	BNE	VOSGA
	LDA	#$FD
	AND	A9005
	STA	A9005
VOSGC	JMP	SCOPEX
;
;	TEST FOR 'LOCK SHIFT MODE' CHARACTER
;
VOSGA	CMP	#$08
	BNE	VOSGB
	LDA	#$80
	ORA	V0291
	STA	V0291
	BMI	VOSGC
;
;	TEST FOR 'UNLOCK SHIFT MODE' CHARACTER
;
VOSGB	CMP	#$09
	BNE	VOSGC
	LDA	#$7F
	AND	V0291
	STA	V0291
	BPL	VOSGC
;.PAGE 'PATCH'
;
;
;
PATCH	INX
	LDA	A0229,X
	ORA	#$80
	STA	A0229,X
	DEX
	LDA	SWRPFL
	CLC
	JMP	RETURN
;.PAGE 'CONSTANTS'
;
;
;
VAED69	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF	; DS	9
	DB	$04
	DB	$FF,$FF,$FF,$FF,$FF	; DS	5
	DB	$E2,$9D,$83,$01
	DB	$FF,$FF,$FF,$FF,$FF	; DS	5
	DB	$91,$A0
	DB	$FF,$FF,$FF,$FF	; DS	4
	DB	$EE,$01,$89,$02
	DB	$FF,$FF,$FF,$FF	; DS	4
	DB	$E1,$FD,$8A
	DB	$FF,$FF,$FF,$FF,$FF	; DS	5
	DB	$B0,$E0,$8B,$F2,$F4,$F6
	DB	$FF,$F0,$ED,$93,$8C,$FF
;
;	CONTROL KEY DEPRESSED
;
KBXLT4	DB	$90,$1C,$9C,$1F,$12
	DB	$FF,$FF,$FF	; DS	3
	DB	$06,$FF,$12
*	DS	45
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
	DB	$FF,$FF,$FF,$FF,$FF
	DB	$05,$9F,$1E,$9E,$92
	DB	$FF,$FF,$FF,$FF	; DS	4
;
;
;
VAEDE4	DB	$05,$19,$16,$2E,$00,$C0
	DB	0,0,0,0,0,0,0,0,0,$1B
;
;
;
RUNLDK	ASC	'LOAD'
	DB	$0D
	ASC	'RUN'
	DB	$0D
;
;
;
SLNADR	DB	0,22,44,66,88,110
	DB	132,154,176,198,220,242
	DB	$08,$1E,$34,$4A,$60,$76
	DB	$8C,$A2,$B8,$CE,$E4
;.PAGE 'THE END'
*	.END
