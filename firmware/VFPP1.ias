;
;	VIC FLOATING POINT ROUTINES  "SVFPP1"  7/13/82
;
SY	JSR	A37B7
	BCC	TH
A3653	JSR	A3904
;
;	FAC = FARG + FAC
;
FPADD	BNE	TE
	JMP	A3A74
TE	LDX	FARG+7
	STX	ZA2+1
	LDX	#>FARG
	LDA	FARG
A3663	TAY
	BEQ	RTSS
	SEC
	SBC	FAC
	BEQ	TH
	BCC	TF
	STY	FAC
	LDY	FARG+5
	STY	FAC+5
	EOR	#$FF
	ADC	#0
	LDY	#0
	STY	ZA2+1
	LDX	#>FAC
	BNE	TG
TF	LDY	#0
	STY	FARG+7
TG	CMP	#$F9
	BMI	SY
	TAY
	LDA	FARG+7
	LSR	ZPSTRT+1,X
	JSR	A37D0
TH	BIT	FARG+6
	BPL	TN
	LDY	#>FAC
	CPX	#>FARG
	BEQ	TJ
	LDY	#>FARG
TJ	SEC
	EOR	#$FF
	ADC	ZA2+1
	STA	FARG+7
	LDA	ZPSTRT+4,Y
	SBC	ZPSTRT+4,X
	STA	FAC+4
	LDA	ZPSTRT+3,Y
	SBC	ZPSTRT+3,X
	STA	FAC+3
	LDA	ZPSTRT+2,Y
	SBC	ZPSTRT+2,X
	STA	FAC+2
	LDA	ZPSTRT+1,Y
	SBC	ZPSTRT+1,X
	STA	FAC+1
A36BE	BCS	A36C3
	JSR	A3765
A36C3	LDY	#0
	TYA
	CLC
TL	LDX	FAC+1
	BNE	TPA
	LDX	FAC+2
	STX	FAC+1
	LDX	FAC+3
	STX	FAC+2
	LDX	FAC+4
	STX	FAC+3
	LDX	FARG+7
	STX	FAC+4
	STY	FARG+7
	ADC	#8
	CMP	#$20
	BNE	TL
A36E3	LDA	#0
A36E5	STA	FAC
A36E7	STA	FAC+5
	RTS
TN	ADC	ZA2+1
	STA	FARG+7
	LDA	FAC+4
	ADC	FARG+4
	STA	FAC+4
	LDA	FAC+3
	ADC	FARG+3
	STA	FAC+3
	LDA	FAC+2
	ADC	FARG+2
	STA	FAC+2
	LDA	FAC+1
	ADC	FARG+1
	STA	FAC+1
	JMP	TQ
TP	ADC	#1
	ASL	FARG+7
	ROL	FAC+4
	ROL	FAC+3
	ROL	FAC+2
	ROL	FAC+1
TPA	BPL	TP
	SEC
	SBC	FAC
	BCS	A36E3
	EOR	#$FF
	ADC	#1
	STA	FAC
TQ	BCC	TW
A3724	INC	FAC
	BEQ	A379C
	ROR	FAC+1
	ROR	FAC+2
	ROR	FAC+3
	ROR	FAC+4
	ROR	FARG+7
TW	RTS
;
;
;
A3765	LDA	FAC+5
	EOR	#$FF
	STA	FAC+5
A376B	LDA	FAC+1
	EOR	#$FF
	STA	FAC+1
	LDA	FAC+2
	EOR	#$FF
	STA	FAC+2
	LDA	FAC+3
	EOR	#$FF
	STA	FAC+3
	LDA	FAC+4
	EOR	#$FF
	STA	FAC+4
	LDA	FARG+7
	EOR	#$FF
	STA	FARG+7
	INC	FARG+7
	BNE	TX
A378D	INC	FAC+4
	BNE	TX
	INC	FAC+3
	BNE	TX
	INC	FAC+2
	BNE	TX
	INC	FAC+1
TX	RTS
;
;
;
A379C	LDX	#15
	JMP	PRERR
;.PAGE
;
;
;
A37A1	LDX	#>Z71+1
TY	LDY	ZPSTRT+4,X
	STY	FARG+7
	LDY	ZPSTRT+3,X
	STY	ZPSTRT+4,X
	LDY	ZPSTRT+2,X
	STY	ZPSTRT+3,X
	LDY	ZPSTRT+1,X
	STY	ZPSTRT+2,X
	LDY	FAC+7
	STY	ZPSTRT+1,X
A37B7	ADC	#8
	BMI	TY
	BEQ	TY
	SBC	#8
	TAY
	LDA	FARG+7
	BCS	UB
TZ	ASL	ZPSTRT+1,X
	BCC	PUP
	INC	ZPSTRT+1,X
PUP	ROR	ZPSTRT+1,X
	ROR	ZPSTRT+1,X
A37D0	ROR	ZPSTRT+2,X
	ROR	ZPSTRT+3,X
	ROR	ZPSTRT+4,X
	ROR	A
	INY
	BNE	TZ
UB	CLC
	RTS
;.PAGE
;
;
;
A3802	DB	$81,$00,$00,$00,$00
;
;
;
A3807	DB	3
	DB	$7F,$5E,$56,$CB,$79
	DB	$80,$13,$9B,$0B,$64
	DB	$80,$76,$38,$93,$16
	DB	$82,$38,$AA,$3B,$20
;
;
;
A381C	DB	$80,$35,$04,$F3,$34
;
;
;
A3821	DB	$81,$35,$04,$F3,$34
;
;
;
A3826	DB	$80,$80,$00,$00,$00
;
;
;
A382B	DB	$80,$31,$72,$17,$F8
;.PAGE
;
;	FAC = LOG(FAC)
;
FPLOG	JSR	A3AA3
	BEQ	UC
	BPL	UD
UC	JMP	A304B
UD	LDA	FAC
	SBC	#$7F
	PHA
	LDA	#$80
	STA	FAC
	LDA	#>A381C
	LDY	#<A381C
	JSR	A3653
	LDA	#>A3821
	LDY	#<A3821
	JSR	A3987
	LDA	#>A3802
	LDY	#<A3802
	JSR	A363C
	LDA	#>A3807
	LDY	#<A3807
	JSR	A3E91
	LDA	#>A3826
	LDY	#<A3826
	JSR	A3653
	PLA
	JSR	A3C0A
	LDA	#>A382B
	LDY	#<A382B
A386E	JSR	A3904
;
;	FAC = FARG * FAC
;
FPMULT	BNE	NRUD
	JMP	RTSU
NRUD	JSR	A392F
	LDA	#0
	STA	Z73
	STA	Z73+1
	STA	Z73+2
	STA	Z73+3
	LDA	FARG+7
	JSR	A389F
	LDA	FAC+4
	JSR	A389F
	LDA	FAC+3
	JSR	A389F
	LDA	FAC+2
	JSR	A389F
	LDA	FAC+1
	JSR	A38A4
	JMP	A3A07
;.PAGE
A389F	BNE	A38A4
	JMP	A37A1
A38A4	LSR	A
	ORA	#$80
UG	TAY
	BCC	UH
	CLC
	LDA	Z73+3
	ADC	FARG+4
	STA	Z73+3
	LDA	Z73+2
	ADC	FARG+3
	STA	Z73+2
	LDA	Z73+1
	ADC	FARG+2
	STA	Z73+1
	LDA	Z73
	ADC	FARG+1
	STA	Z73
UH	ROR	Z73
	ROR	Z73+1
	ROR	Z73+2
	ROR	Z73+3
	ROR	FARG+7
	TYA
	LSR	A
	BNE	UG
RTSU	RTS
;.PAGE
;
;	MEMORY TO FARG
;
A3904	STA	Z6F
	STY	Z6F+1
	LDY	#4
	LDA	(Z6F),Y
	STA	FARG+4
	DEY
	LDA	(Z6F),Y
	STA	FARG+3
	DEY
	LDA	(Z6F),Y
	STA	FARG+2
	DEY
	LDA	(Z6F),Y
	STA	FARG+5
	EOR	FAC+5
	STA	FARG+6
	LDA	FARG+5
	ORA	#$80
	STA	FARG+1
	DEY
	LDA	(Z6F),Y
	STA	FARG
	LDA	FAC
	RTS
;.PAGE
A392F	LDA	FARG
A3931	BEQ	UU
	CLC
	ADC	FAC
	BCC	US
	BMI	UV
	CLC
	DB	$2C
US	BPL	UU
	ADC	#$80
	STA	FAC
	BNE	UT
	JMP	A36E7
UT	LDA	FARG+6
	STA	FAC+5
	RTS
;
;
;
A394C	LDA	FAC+5
	EOR	#$FF
	BMI	UV
UU	PLA
	PLA
	JMP	A36E3
UV	JMP	A379C
A395A	JSR	A3A84
	TAX
	BEQ	UW
	CLC
	ADC	#2
	BCS	UV
AD9BF	LDX	#0
	STX	FARG+6
	JSR	A3663
	INC	FAC
	BEQ	UV
UW	RTS
;
;
;
A3971	DB	$84,$20,0,0,0
A3976	JSR	A3A84
	LDA	#>A3971
	LDY	#<A3971
	LDX	#0
A397F	STX	FARG+6
	JSR	MFPACC
	JMP	FPDIV
;.PAGE
A3987	JSR	A3904
;
;	FAC = FARG / FAC
;
FPDIV	BEQ	VE
	JSR	ROUNDA
	LDA	#0
	SEC
	SBC	FAC
	STA	FAC
	JSR	A392F
	INC	FAC
	BEQ	UV
	LDX	#$FC
	LDA	#$01
UX	LDY	FARG+1
	CPY	FAC+1
	BNE	UY
	LDY	FARG+2
	CPY	FAC+2
	BNE	UY
	LDY	FARG+3
	CPY	FAC+3
	BNE	UY
	LDY	FARG+4
	CPY	FAC+4
UY	PHP
	ROL	A
	BCC	UZ
	INX
	STA	Z75+1,X
	BEQ	VC
	BPL	VD
	LDA	#1
UZ	PLP
	BCS	VB
VA	ASL	FARG+4
	ROL	FARG+3
	ROL	FARG+2
	ROL	FARG+1
	BCS	UY
	BMI	UX
	BPL	UY
VB	TAY
	LDA	FARG+4
	SBC	FAC+4
	STA	FARG+4
	LDA	FARG+3
	SBC	FAC+3
	STA	FARG+3
	LDA	FARG+2
	SBC	FAC+2
	STA	FARG+2
	LDA	FARG+1
	SBC	FAC+1
	STA	FARG+1
	TYA
	JMP	VA
VC	LDA	#$40
	BNE	UZ
VD	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	STA	FARG+7
	PLP
	JMP	A3A07
;
;
;
VE	LDX	#20
	JMP	PRERR
;
;
;
A3A07	LDA	Z73
	STA	FAC+1
	LDA	Z73+1
	STA	FAC+2
	LDA	Z73+2
	STA	FAC+3
	LDA	Z73+3
	STA	FAC+4
	JMP	A36C3
;.PAGE
;
;	MOVE MEMORY TO FAC
;
MFPACC	STA	Z6F
	STY	Z6F+1
	LDY	#4
	LDA	(Z6F),Y
	STA	FAC+4
	DEY
	LDA	(Z6F),Y
	STA	FAC+3
	DEY
	LDA	(Z6F),Y
	STA	FAC+2
	DEY
	LDA	(Z6F),Y
	STA	FAC+5
	ORA	#$80
	STA	FAC+1
	DEY
	LDA	(Z6F),Y
	STA	FAC
	STY	FARG+7
	RTS
;.PAGE
A3A3F	LDX	#>ZA9
	DB	$2C
A3A42	LDX	#>ZA4
	LDY	#0
	BEQ	A3A4C
A3A48	LDX	Z96
	LDY	Z96+1
;
;	MOVE FAC TO MEMORY
;
A3A4C	JSR	ROUNDA
	STX	Z6F
	STY	Z6F+1
	LDY	#4
	LDA	FAC+4
	STA	(Z6F),Y
	DEY
	LDA	FAC+3
	STA	(Z6F),Y
	DEY
	LDA	FAC+2
	STA	(Z6F),Y
	DEY
	LDA	FAC+5
	ORA	#$7F
	AND	FAC+1
	STA	(Z6F),Y
	DEY
	LDA	FAC
	STA	(Z6F),Y
	STY	FARG+7
	RTS
;.PAGE
;
;	MOVE FARG TO FAC
;
A3A74	LDA	FARG+5
A3A76	STA	FAC+5
	LDX	#5
TA	LDA	FARG-1,X
	STA	FAC-1,X
	DEX
	BNE	TA
	STX	FARG+7
	RTS
;
;	MOVE FAC TO FARG WITH ROUNDING
;
A3A84	JSR	ROUNDA
;
;	MOVE FAC TO FARG WITHOUT ROUNDING
;
A3A87	LDX	#6
TB	LDA	FAC-1,X
	STA	FARG-1,X
	DEX
	BNE	TB
	STX	FARG+7
RTST	RTS
;
;	ROUND OFF FAC
;
ROUNDA	LDA	FAC
	BEQ	RTST
	ASL	FARG+7
	BCC	RTST
A3A9B	JSR	A378D
	BNE	RTST
	JMP	A3724
;
;	MOVE SIGN OF FAC TO 'A'
;
A3AA3	LDA	FAC
	BEQ	TC
A3AA7	LDA	FAC+5
A3AA9	ROL	A
	LDA	#$FF
	BCS	TC
	LDA	#1
TC	RTS
;
;	FAC = SGN(FAC)
;
A3AB1	JSR	A3AA3
A3AB4	STA	FAC+1
	LDA	#0
	STA	FAC+2
	LDX	#$88
A3ABC	LDA	FAC+1
	EOR	#$FF
	ROL	A
A3AC1	LDA	#0
	STA	FAC+4
	STA	FAC+3
ADB21	STX	FAC
	STA	FARG+7
	STA	FAC+5
	JMP	A36BE
;
;	FAC = ABS(FAC)
;
A3AD0	LSR	FAC+5
	RTS
;.PAGE
A3AD3	STA	Z71
A3AD5	STY	Z71+1
	LDY	#0
	LDA	(Z71),Y
	INY
	TAX
	BEQ	A3AA3
	LDA	(Z71),Y
	EOR	FAC+5
	BMI	A3AA7
	CPX	FAC
	BNE	KX
	LDA	(Z71),Y
	ORA	#$80
	CMP	FAC+1
	BNE	KX
	INY
	LDA	(Z71),Y
	CMP	FAC+2
	BNE	KX
	INY
	LDA	(Z71),Y
	CMP	FAC+3
	BNE	KX
	INY
	LDA	#$7F
	CMP	FARG+7
	LDA	(Z71),Y
	SBC	FAC+4
	BEQ	RTSN
KX	LDA	FAC+5
	BCC	KY
	EOR	#$FF
KY	JMP	A3AA9
;.PAGE
A3B13	LDA	FAC
	BEQ	A3B61
	SEC
	SBC	#$A0
	BIT	FAC+5
	BPL	KZ
	TAX
	LDA	#$FF
	STA	FAC+7
	JSR	A376B
	TXA
KZ	LDX	#>FAC
	CMP	#$F9
	BPL	JN
	JSR	A37B7
	STY	FAC+7
RTSN	RTS
JN	TAY
	LDA	FAC+5
	AND	#$80
	LSR	FAC+1
	ORA	FAC+1
	STA	FAC+1
	JSR	A37D0
	STY	FAC+7
	RTS
;
;	FAC = INT(FAC)
;
A3B44	LDA	FAC
	CMP	#$A0
	BCS	RTSR
	JSR	A3B13
	STY	FARG+7
	LDA	FAC+5
	STY	FAC+5
	EOR	#$80
	ROL	A
	LDA	#$A0
	STA	FAC
	LDA	FAC+4
	STA	Z0A
	JMP	A36BE
;
;
;
A3B61	STA	FAC+1
	STA	FAC+2
	STA	FAC+3
	STA	FAC+4
	TAY
RTSR	RTS
;.PAGE
ASC2FP	LDY	#0
	LDX	#10
PM	STY	ZAA,X
	DEX
	BPL	PM
	BCC	PQ
	CMP	#'-
	BNE	PN
	STX	FAC+6
	BEQ	A3B82
PN	CMP	#'+
	BNE	ST
A3B82	JSR	NXTTCE
PQ	BCC	A3BF6
ST	CMP	#'.
	BEQ	PX
	CMP	#'E
	BNE	SJ
	JSR	NXTTCE
	BCC	PU
	CMP	#$AB	; - TOKEN
	BEQ	PR
	CMP	#'-
	BEQ	PR
	CMP	#$AA	; + TOKEN
	BEQ	A3BB2
	CMP	#'+
	BEQ	A3BB2
	BNE	PW
PR	ROR	ZAC+1
A3BB2	JSR	NXTTCE
PU	BCC	A3C1D
PW	BIT	ZAC+1
	BPL	SJ
	LDA	#0
	SEC
	SBC	ZAA+1
	JMP	SK
PX	ROR	ZAC
	BIT	ZAC
	BVC	A3B82
SJ	LDA	ZAA+1
SK	SEC
	SBC	ZAA
	STA	ZAA+1
	BEQ	SN
	BPL	SM
SL	JSR	A3976
	INC	ZAA+1
	BNE	SL
	BEQ	SN
SM	JSR	A395A
	DEC	ZAA+1
	BNE	SM
SN	LDA	FAC+6
	BMI	SP
	RTS
SP	JMP	A3E05
;.PAGE
A3BF6	PHA
	BIT	ZAC
	BPL	SQ
	INC	ZAA
SQ	JSR	A395A
	PLA
	SEC
	SBC	#$30
	JSR	A3C0A
	JMP	A3B82
;
;
;
A3C0A	PHA
	JSR	A3A84
	PLA
	JSR	A3AB4
	LDA	FARG+5
	EOR	FAC+5
	STA	FARG+6
	LDX	FAC
	JMP	FPADD
;
;
;
A3C1D	LDA	ZAA+1
	CMP	#10
	BCC	SR
	LDA	#$64
	BIT	ZAC+1
	BMI	PSS
	JMP	A379C
SR	ASL	A
	ASL	A
	CLC
	ADC	ZAA+1
	ASL	A
	CLC
	LDY	#0
	ADC	(CTXPTR),Y
	SEC
	SBC	#$30
PSS	STA	ZAA+1
	JMP	A3BB2
;.PAGE
A3C3F	DB	$9B,$3E,$BC,$1F,$FD
A3C44	DB	$9E,$6E,$6B,$27,$FD
A3C49	DB	$9E,$6E,$6B,$28,0
